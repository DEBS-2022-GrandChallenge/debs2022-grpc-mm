plugins {
    id "org.springframework.boot" version "2.7.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.google.protobuf" version "0.8.18"
    id "java"
    id "idea"
}

repositories {
    mavenCentral()
}

bootJar { enabled = false }
jar { enabled = true }

subprojects {
    group = "org.debs.group4"
    version = "1.0-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.google.protobuf"
    apply plugin: "java"
    apply plugin: "idea"

    def grpcVersion = "1.24.1" // CURRENT_GRPC_VERSION
    def protobufVersion = "3.9.2"
    def protocVersion = protobufVersion
    def springBootGrpcVersion = "2.13.1.RELEASE"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // Spring boot
        implementation "org.springframework.boot:spring-boot-starter"
        testImplementation "org.springframework.boot:spring-boot-starter-test"

        // Lombok
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"

        // gRPC + gRPC Spring boot starter
        implementation "io.grpc:grpc-netty-shaded"
        implementation "io.grpc:grpc-protobuf"
        implementation "io.grpc:grpc-stub"
        implementation "net.devh:grpc-spring-boot-starter:${springBootGrpcVersion}"
        if (JavaVersion.current().isJava9Compatible()) {
            implementation "jakarta.annotation:jakarta.annotation-api"
        }
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${protocVersion}"
        }
        generatedFilesBaseDir = "$projectDir/src/generated"
        clean {
            delete generatedFilesBaseDir
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs "build/generated/source/proto/main/grpc"
                srcDirs "build/generated/source/proto/main/java"
            }
        }
    }

    idea {
        module {
            sourceDirs += file("src/generated/main/java")
            sourceDirs += file("src/generated/main/grpc")
            generatedSourceDirs += file("src/generated/main/java")
            generatedSourceDirs += file("src/generated/main/grpc")
        }
    }

    tasks.named("test") {
        useJUnitPlatform()
    }
}

project(":common") {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(":server") {
    bootJar { enabled = true }
    jar { enabled = true }
}

project(":client") {
    bootJar { enabled = true }
    jar { enabled = true }
}